# Non regression tests 

function(yabmp_add_test file)
	set(CONVERT_FAILS FALSE)
	set(INFO_FAILS FALSE)
	
	if (${ARGC} GREATER 1)
		set(CONVERT_FAILS ${ARGV1})
		if (${ARGC} GREATER 2)
			set(INFO_FAILS ${ARGV2})
		endif()
	endif()
	
	set(INPUT ${CMAKE_CURRENT_SOURCE_DIR}/input/${file})
	get_filename_component(OUTPUTDIR ${CMAKE_CURRENT_BINARY_DIR}/${file} DIRECTORY)
	if (NOT EXISTS "${OUTPUTDIR}")
		file(MAKE_DIRECTORY "${OUTPUTDIR}")
	endif()
	
	add_test(NAME ${file}-info COMMAND yabmpinfo -o "${CMAKE_CURRENT_BINARY_DIR}/${file}.info.txt" "${INPUT}" )
	set_tests_properties(${file}-info PROPERTIES WILL_FAIL ${INFO_FAILS})
	if (NOT INFO_FAILS)
		add_test(NAME ${file}-info-compare COMMAND "${CMAKE_COMMAND}" -E compare_files "${CMAKE_CURRENT_SOURCE_DIR}/expected/${file}.info.txt" "${CMAKE_CURRENT_BINARY_DIR}/${file}.info.txt")
		set_tests_properties(${file}-info-compare PROPERTIES DEPENDS ${file}-info)
	endif()
	
	add_test(NAME ${file}-convert COMMAND yabmpconvert -i "${INPUT}" -o "${CMAKE_CURRENT_BINARY_DIR}/${file}.png")
	set_tests_properties(${file}-convert PROPERTIES WILL_FAIL ${CONVERT_FAILS})
	if (NOT CONVERT_FAILS)
		add_test(NAME ${file}-convert-compare COMMAND "${CMAKE_COMMAND}" -E compare_files "${CMAKE_CURRENT_SOURCE_DIR}/expected/${file}.png" "${CMAKE_CURRENT_BINARY_DIR}/${file}.png")
		set_tests_properties(${file}-convert-compare PROPERTIES DEPENDS ${file}-convert)
	endif()
endfunction()


# bmpsuite good tests
yabmp_add_test("bmpsuite/g/pal1.bmp")
yabmp_add_test("bmpsuite/g/pal1bg.bmp")
yabmp_add_test("bmpsuite/g/pal1wb.bmp")
yabmp_add_test("bmpsuite/g/pal4.bmp")
yabmp_add_test("bmpsuite/g/pal4rle.bmp")
yabmp_add_test("bmpsuite/g/pal8-0.bmp")
yabmp_add_test("bmpsuite/g/pal8.bmp")
yabmp_add_test("bmpsuite/g/pal8nonsquare.bmp")
yabmp_add_test("bmpsuite/g/pal8os2.bmp")
yabmp_add_test("bmpsuite/g/pal8rle.bmp")
yabmp_add_test("bmpsuite/g/pal8topdown.bmp")
yabmp_add_test("bmpsuite/g/pal8v4.bmp")
yabmp_add_test("bmpsuite/g/pal8v5.bmp")
yabmp_add_test("bmpsuite/g/pal8w124.bmp")
yabmp_add_test("bmpsuite/g/pal8w125.bmp")
yabmp_add_test("bmpsuite/g/pal8w126.bmp")
yabmp_add_test("bmpsuite/g/rgb16-565.bmp")
yabmp_add_test("bmpsuite/g/rgb16-565pal.bmp")
yabmp_add_test("bmpsuite/g/rgb16.bmp")
yabmp_add_test("bmpsuite/g/rgb24.bmp")
yabmp_add_test("bmpsuite/g/rgb24pal.bmp")
yabmp_add_test("bmpsuite/g/rgb32.bmp")
yabmp_add_test("bmpsuite/g/rgb32bf.bmp")

# bmpsuite questionable tests
yabmp_add_test("bmpsuite/q/pal1p1.bmp")
yabmp_add_test("bmpsuite/q/pal2.bmp")
yabmp_add_test("bmpsuite/q/pal4rletrns.bmp")
yabmp_add_test("bmpsuite/q/pal8offs.bmp")
yabmp_add_test("bmpsuite/q/pal8os2sp.bmp")
yabmp_add_test("bmpsuite/q/pal8os2v2-16.bmp" TRUE TRUE)
yabmp_add_test("bmpsuite/q/pal8os2v2.bmp" TRUE TRUE)
yabmp_add_test("bmpsuite/q/pal8oversizepal.bmp")
yabmp_add_test("bmpsuite/q/pal8rletrns.bmp")
yabmp_add_test("bmpsuite/q/rgb16-231.bmp")
yabmp_add_test("bmpsuite/q/rgb24jpeg.bmp" TRUE)
yabmp_add_test("bmpsuite/q/rgb24largepal.bmp")
yabmp_add_test("bmpsuite/q/rgb24lprof.bmp")
yabmp_add_test("bmpsuite/q/rgb24png.bmp" TRUE)
yabmp_add_test("bmpsuite/q/rgb24prof.bmp")
yabmp_add_test("bmpsuite/q/rgb32-111110.bmp")
yabmp_add_test("bmpsuite/q/rgb32fakealpha.bmp")
yabmp_add_test("bmpsuite/q/rgba16-4444.bmp")
yabmp_add_test("bmpsuite/q/rgba32.bmp")
yabmp_add_test("bmpsuite/q/rgba32abf.bmp" TRUE)

# bmpsuite bad tests
yabmp_add_test("bmpsuite/b/badbitcount.bmp" TRUE)
yabmp_add_test("bmpsuite/b/badbitssize.bmp")
yabmp_add_test("bmpsuite/b/baddens1.bmp")
yabmp_add_test("bmpsuite/b/baddens2.bmp")
yabmp_add_test("bmpsuite/b/badfilesize.bmp")
yabmp_add_test("bmpsuite/b/badheadersize.bmp" TRUE TRUE)
yabmp_add_test("bmpsuite/b/badpalettesize.bmp" TRUE TRUE)
yabmp_add_test("bmpsuite/b/badplanes.bmp")
yabmp_add_test("bmpsuite/b/badrle.bmp" TRUE)
yabmp_add_test("bmpsuite/b/badwidth.bmp" TRUE)
yabmp_add_test("bmpsuite/b/pal8badindex.bmp" TRUE)
yabmp_add_test("bmpsuite/b/reallybig.bmp" TRUE)
yabmp_add_test("bmpsuite/b/rletopdown.bmp" TRUE)
yabmp_add_test("bmpsuite/b/shortfile.bmp" TRUE)
